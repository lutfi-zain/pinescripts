// This Pine Scriptâ„¢ code is subject to the terms of the Mozilla Public License 2.0 at https://mozilla.org/MPL/2.0/
// Â© 0xBujang
// This code is originally authored by QuantAlgo

//@version=6
strategy(title="0xBujang - Ichimoku SokudÅ For Loop", overlay=true, default_qty_type = strategy.percent_of_equity, default_qty_value = 99 , initial_capital = 10000)

//              â•”â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•—              //
//              â•‘      USER-DEFINED SETTINGS     â•‘              //
//              â•šâ•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•              //

// Input Groups
var string zlag_settings    = "â•â•â•â•â•â•â•â• Zero Lag Settings â•â•â•â•â•â•â•â•"
var string loop_settings    = "â•â•â•â•â•â•â•â• For Loop Settings â•â•â•â•â•â•â•â•"
var string thresh_settings  = "â•â•â•â•â•â•â•â• Threshold Settings â•â•â•â•â•â•â•â•"
var string visual_settings  = "â•â•â•â•â•â•â•â• Visualization Settings â•â•â•â•â•â•â•â•"
var string strategy_settings  = "â•â•â•â•â•â•â•â• Strategy Settings â•â•â•â•â•â•â•â•"
var string trenddetect_settings = "â•â•â•â•â•â•â•â• Trend Detection Settings (Not Ready: Under Development) â•â•â•â•â•â•â•â•"


// Tooltips
tooltip_zl_length     = "Length of the Zero Lag calculation period. Higher values create smoother signals."
tooltip_vol_mult      = "Multiplier for volatility in signal generation. Higher values make signals more conservative by requiring larger price movements."
tooltip_loop_start    = "Starting point for the loop analysis. Lower values analyze more recent price action."
tooltip_loop_end      = "Ending point for the loop analysis. Higher values analyze longer historical periods."
tooltip_thresh_up     = "Minimum score required to generate uptrend signals. Higher values create stricter conditions."
tooltip_thresh_down   = "Maximum score required to generate downtrend signals. Lower values create stricter conditions."
tooltip_signals       = "Enable/disable signal markers on the chart"
tooltip_candles       = "Enable/disable candle coloring based on trend direction"
tooltip_bg_lines      = "Enable/disable vertical lines on signal changes"

// Zero Lag Settings
length = input.int(50, "Zero Lag Length", 
     minval=1, 
     group=zlag_settings, 
     tooltip=tooltip_zl_length)
volatility_mult = input.float(0.9, "Volatility Multiplier", 
     minval=0.1, 
     group=zlag_settings, 
     tooltip=tooltip_vol_mult)

// Loop Settings
loop_start = input.int(1, "Loop Start", 
     minval=1, 
     group=loop_settings, 
     tooltip=tooltip_loop_start)
loop_end = input.int(70, "Loop End", 
     minval=1, 
     group=loop_settings, 
     tooltip=tooltip_loop_end)

// Threshold Settings
threshold_up = input.int(1, "Threshold Uptrend", 
     group=thresh_settings, 
     tooltip=tooltip_thresh_up)
threshold_down = input.int(-1, "Threshold Downtrend", 
     group=thresh_settings, 
     tooltip=tooltip_thresh_down)

// Visualization Settings

bullcolor = input.color(#ffdbac, "Bullish Color", group=visual_settings)
bearcolor = input.color(#96a4d3, "Bearish Color", group=visual_settings)
show_signals = input.bool(true, "Show Signal Markers", 
     group=visual_settings, tooltip=tooltip_signals)
show_trend_basis = input.bool(true, "Show Trend Basis", 
     group=visual_settings, tooltip=tooltip_signals)
paint_candles = input.bool(true, "Color Candles", 
     group=visual_settings, tooltip=tooltip_candles)
show_bg_lines = input.bool(false, "Signal Change Lines", 
     group=visual_settings, tooltip=tooltip_bg_lines)

// Strategy Settings
show_long   = input.bool(true,"Long Signal", group = strategy_settings)
show_short   = input.bool(false,"Short Signal", group = strategy_settings)
limit_backtest_time = input.bool(true,"Limit Backtest Time", group = strategy_settings)
backtest_start_at = input.time(defval = timestamp('01 Jan 2018 00:00 +0700'), title="Backtest Start At", group = strategy_settings)
reset_trend = input.bool(false, "Reset Trend When No Signal", group=strategy_settings)

// Ichimoku Input
show_ichimoku = input(false, title="Show Ichimoku Cloud")
ichimoku_sokudo = input(false, title="Activate Ichimoku Sokudo")
conversionPeriods = input.int(9, minval=1, title="Conversion Line Length")
basePeriods = input.int(26, minval=1, title="Base Line Length")
laggingSpan2Periods = input.int(52, minval=1, title="Leading Span B Length")
displacement = input.int(26, minval=1, title="Lagging Span")
donchian(len) => math.avg(ta.lowest(len), ta.highest(len))
conversionLine = donchian(conversionPeriods)
baseLine = donchian(basePeriods)
leadLine1 = math.avg(conversionLine, baseLine)
leadLine2 = donchian(laggingSpan2Periods)

// Trend Detection Settings
use_lr_slope = input.bool(false, "Use Linear Regression Slope", group=trenddetect_settings)
slope_len = input.int(14, "LR Slope Length", group=trenddetect_settings)

use_fdi = input.bool(false, "Use Fractal Dimension Index", group=trenddetect_settings)
fdi_len = input.int(21, "FDI Length", group=trenddetect_settings)

use_cmo_roc = input.bool(false, "Use CMO + ROC Combo", group=trenddetect_settings)
roc_len = input.int(5, "ROC Length", group=trenddetect_settings)
cmo_len = input.int(9, "CMO Length", group=trenddetect_settings)


//              â•”â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•—              //
//              â•‘      ZERO LAG CALCULATIONS     â•‘              //
//              â•šâ•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•              //

lag = math.floor((length - 1) / 2)
zl_basis = ta.ema(close + (close - close[lag]), length)
volatility = ta.highest(ta.atr(length), length*3) * volatility_mult

//              â•”â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•—              //
//              â•‘        FOR LOOP ANALYSIS       â•‘              //
//              â•šâ•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•              //

forloop_analysis(basis_price) =>
    sum = 0.0
    for i = loop_start to loop_end by 1
        sum += (basis_price > basis_price[i] ? 1 : -1)
    sum

score = forloop_analysis(zl_basis)

//              â•”â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•—              //
//              â•‘        TREND ANALYSIS          â•‘              //
//              â•šâ•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•              //

// Linear Regression Slope
slope = ta.linreg(close, slope_len, 0) - ta.linreg(close, slope_len, 1)
is_trending_lr = math.abs(slope) > 0.0

// Fractal Dimension Index (FDI)
hh = ta.highest(high, fdi_len)
ll = ta.lowest(low, fdi_len)
n = 0.0
for i = 0 to fdi_len - 2
    n += math.abs(close[i] - close[i + 1])
n /= (fdi_len - 1)
fdi = math.log10(n) / math.log10(hh - ll)
is_trending_fdi = fdi < 1.4

// Chande Momentum Oscillator + Rate of Change (CMO + ROC Combo)
roc_val = ta.roc(close, roc_len)
cmo_val = ta.cmo(close, cmo_len)
is_trending_combo = math.abs(roc_val) > 1 and math.abs(cmo_val) > 30

isTrending = (
     (use_lr_slope and is_trending_lr) or 
     (use_fdi and is_trending_fdi) or
     (use_cmo_roc and is_trending_combo)
     )

// log.info("isTrending: " + str.tostring(isTrending))

isTrendingIndicatorActive = (use_lr_slope or use_cmo_roc or use_fdi)

// Ichimoku
currentSSA = leadLine1[26]
currentSSB = leadLine2[26]

kumoHigh = math.max(currentSSA, currentSSB)
kumoLow  = math.min(currentSSA, currentSSB)

bodyLow  = math.min(open, close)
bodyHigh = math.max(open, close)

// ðŸŸ¢/ðŸ”´ Kumo "Color" logic (SSA above SSB is green, otherwise red)
is_current_kumo_green = currentSSA > currentSSB
is_current_kumo_red   = currentSSA < currentSSB

is_kumo_green = leadLine1 > leadLine2  // 26 bars ahead
is_kumo_red   = leadLine1 < leadLine2

// âœ… Body positions relative to current Kumo (SSA & SSB from 26 bars ago)
is_whole_body_inside_current_kumo = bodyLow >= kumoLow and bodyHigh <= kumoHigh
is_whole_body_above_current_kumo  = bodyLow > kumoHigh
is_whole_body_under_current_kumo  = bodyHigh <= kumoLow

// âœ… Tenkan-sen x Kijun-sen
is_tk_above_kj =  baseLine > conversionLine
is_whole_body_under_tkkj = open <= math.min(conversionLine,baseLine) and close < math.min(conversionLine,baseLine)

//              â•”â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•—              //
//              â•‘        SIGNAL GENERATION       â•‘              //
//              â•šâ•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•              //

// Long/Short conditions
long_signal = score > threshold_up and close > zl_basis + volatility
short_signal = score < threshold_down and close < zl_basis - volatility

// Trend detection
var trend = 0
if long_signal
    trend := 1
else if short_signal
    if ichimoku_sokudo 
        if is_current_kumo_green and is_whole_body_under_current_kumo // === âš ï¸ Prevent Close if Still in Kouten Trend
            trend := -1
    else 
        trend := -1
else if reset_trend
    trend := 0

// === âš ï¸ Early Close for Sanyaku Gyakuten
if ichimoku_sokudo and is_kumo_red and is_whole_body_under_current_kumo and is_whole_body_under_tkkj and is_tk_above_kj
    trend := -1

// Track trend changes
var prev_trend = 0
trend_changed = trend != prev_trend
prev_trend := trend


// Condition
longCondition = (trend_changed and trend == 1) and (limit_backtest_time ? time > backtest_start_at : true)
shortCondition = (trend_changed and trend == -1) and (limit_backtest_time ? time > backtest_start_at : true)

finalLongCondition =  (isTrendingIndicatorActive ? isTrending and longCondition : longCondition)
finalShortCondition =  (isTrendingIndicatorActive ? isTrending  and shortCondition : shortCondition)

//              â•”â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•—              //
//              â•‘             STRATEGY           â•‘              //
//              â•šâ•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•              //

if longCondition
    if show_long
        strategy.entry("Long", strategy.long)
    if show_short
        strategy.close("Short")

// === âš ï¸ Early Close for Sanyaku Gyakuten
if ichimoku_sokudo and is_kumo_red and is_whole_body_under_current_kumo and is_whole_body_under_tkkj and is_tk_above_kj
    if show_long
        strategy.close("Long")

if shortCondition
    if show_long
        if ichimoku_sokudo 
            if is_current_kumo_green and is_whole_body_under_current_kumo // === âš ï¸ Prevent Close if Still in Kouten Trend
                strategy.close("Long")
        else 
            strategy.close("Long")
    if show_short
        strategy.entry("Short", strategy.short)

longConditionSokudo = longCondition and ichimoku_sokudo ? true : longCondition

//              â•”â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•—              //
//              â•‘             ALERTS             â•‘              //
//              â•šâ•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•              //

alertcondition(ta.crossover(trend, 0),
     title="Zero Lag Signals Long",
     message="Zero Lag Signals Long {{exchange}}:{{ticker}}")

alertcondition(ta.crossunder(trend, 0),
     title="Zero Lag Signals Short",
     message="Zero Lag Signals Short {{exchange}}:{{ticker}}")

//              â•”â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•—              //
//              â•‘         VISUALIZATION          â•‘              //
//              â•šâ•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•              //

// Current trend color
trend_col = trend == 1 ? bullcolor : trend == -1 ? bearcolor : na

// Plot Zero Lag line
p_basis = plot(show_trend_basis ? zl_basis : na, "Zero Lag Basis", color=trend_col, linewidth=3)
p_price = plot(hl2, "Price", display=display.none, editable=false)

// Fill between Zero Lag and price
fill(p_basis, p_price, hl2, show_trend_basis ? zl_basis : na, na, color.new(trend_col, 20))

// Plot trend shift labels
plotshape(longCondition and show_signals ? zl_basis : na, "Bullish Trend", 
     shape.labelup, location.absolute, bullcolor, 
     text="ð‘³", textcolor=#000000, size=size.small, force_overlay=true)

plotshape(shortCondition and show_signals ? zl_basis : na, "Bearish Trend", 
     shape.labeldown, location.absolute, bearcolor, 
     text="ð‘º", textcolor=#ffffff, size=size.small, force_overlay=true)

// Background signal lines
bgcolor(show_bg_lines ? 
     (ta.crossover(trend, 0) ? bullcolor : 
      ta.crossunder(trend, 0) ? bearcolor : na) : na)

// Color candles based on trend
barcolor(paint_candles ? 
     (trend == 1 ? bullcolor : 
      trend == -1 ? bearcolor : na) : na)

// Ichimoku
plot(show_ichimoku ? conversionLine : na, color=#2962FF, title="Conversion Line")
plot(show_ichimoku ? baseLine : na, color=#B71C1C, title="Base Line")
plot(show_ichimoku ? close : na, offset = -displacement + 1, color=#43A047, title="Lagging Span")
p1 = plot(show_ichimoku ? leadLine1 : na, offset = displacement - 1, color=#A5D6A7,title="Leading Span A")
p2 = plot(show_ichimoku ? leadLine2 : na , offset = displacement - 1, color=#EF9A9A,title="Leading Span B")
plot(show_ichimoku ? (leadLine1 > leadLine2 ? leadLine1 : leadLine2) : na, offset = displacement - 1, title = "Kumo Cloud Upper Line", display = display.none) 
plot(show_ichimoku ? (leadLine1 < leadLine2 ? leadLine1 : leadLine2) : na, offset = displacement - 1, title = "Kumo Cloud Lower Line", display = display.none) 
fill(p1, p2, color = leadLine1 > leadLine2 ? color.rgb(67, 160, 71, 90) : color.rgb(244, 67, 54, 90))

//              â•”â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•—              //
//              â•‘        GENERAL FUNCTION        â•‘              //
//              â•šâ•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•              //

// Logging function
// log_event(_price, _object, _toLabel=false) =>
//     // Format timestamp
//     ts = str.tostring(year) + "-" + str.tostring(month, "00") + "-" + str.tostring(dayofmonth, "00") + " " + str.tostring(hour, "00") + ":" + str.tostring(minute, "00") + ":" + str.tostring(second, "00")

//     msg = ts + " | " + str.tostring(_price, format.mintick) + " | " + str.tostring(_object)

//     if _toLabel
//         label.new(bar_index, low, text=msg, style=label.style_label_up, size=size.small, textcolor=color.white, color=color.gray)
//     else    
//         log.info(msg)


// // Usage
// log_event(close, "Long Signal Triggered")